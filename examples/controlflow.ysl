/* for all control flow, the following values are falsey (all others are truthy):
 * false
 * undef
 * '' (empty string)
 * nan
 */

x := undef
i := 0

# if statements:
if x === true {
    echo 'x is true'
} elseif x === false {
    echo 'x is false'
} else {
    echo 'x is not a boolean'
}

## Expressions

# `&&` and `||` short circuit, and return either the left or right value:
echo x && true
echo x || true


# `?:` works as expected:
name := 'YASL'
echo name->startswith('A') ? 'starts with A' :
      name->startswith('T') ? 'starts with T' :
      name->startswith('Y') ? 'starts with Y' :
      'i do not recognise that letter'


# `??` returns the right value iff the left is undef, else returns the left value.
# used to set default values for variables.

echo undef ?? 'default'
echo 'YASL' ?? 'default'

## Loops (note that `continue` and `break` also work as expected

# while loops:
while i < 10 {
    echo i
    i += 1
}

# numeric for loops:
for i := 0; i < 10; i += 1 {
    echo i
}

# iterative for loops:
for letter <- ['Y', 'A', 'S', 'L'] {
    echo letter
}
